#-------------------------------------------------------------------------------
# Interactive environment configs.
#-------------------------------------------------------------------------------
setopt nullglob extendedglob


#-------------------------------------------------------------------------------
# Shortcut to this dotfiles path is $DOTFILES
#-------------------------------------------------------------------------------
export DOTFILES="$HOME/.dotfiles"


#-------------------------------------------------------------------------------
# Stash your environment variables in ~/.localrc. 
# This means they'll stay out of your main dotfiles
# repository (which may be public, like this one), 
# but you'll have access to them in your scripts.
#-------------------------------------------------------------------------------
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi


#-------------------------------------------------------------------------------
# All of our non-path/env/profile/login/logout zsh files
# => basically any directory that contains a zsh file
#-------------------------------------------------------------------------------
typeset -U fpath_files interactive_files completion_files
fpath_files=($DOTFILES/**/fpath.zsh)
interactive_files=($DOTFILES/**/*.zsh~**/completion.zsh~**/path.zsh~**/env.zsh~**/profile.zsh~**/fpath.zsh~**/login.zsh~**/logout.zsh)
completion_files=($DOTFILES/**/completion.zsh)


#-------------------------------------------------------------------------------
# Load fpath config files
#-------------------------------------------------------------------------------
for file in ${fpath_files}; do
  source "$file"
done


#-------------------------------------------------------------------------------
# Initialize Zsh completion system; this needs to be
# initialized before we source our interctive files
# so that `compdef` can work
#-------------------------------------------------------------------------------
autoload -Uz compinit
compinit


#-------------------------------------------------------------------------------
# Load interactive files (everything but the completion, fpath, env, profile, 
# login, and logout files)
#-------------------------------------------------------------------------------
for file in ${interactive_files}; do
  source "$file"
done


#-------------------------------------------------------------------------------
# Initialize autocomplete here, otherwise functions won't be loaded
#-------------------------------------------------------------------------------
autoload bashcompinit && bashcompinit
autoload -Uz compinit && compinit


#-------------------------------------------------------------------------------
# Load every completion after autocomplete loads
#-------------------------------------------------------------------------------
for file in ${completion_files}; do
  source "$file"
done

unset fpath_files interactive_files completion_files
unsetopt nullglob extendedglob
setopt no_complete_aliases


#-------------------------------------------------------------------------------
# Better history, credit to: 
# https://coderwall.com/p/jpj_6q/zsh-better-history-searching-with-arrow-keys
#-------------------------------------------------------------------------------
autoload -U up-line-or-beginning-search
autoload -U down-line-or-beginning-search
zle -N up-line-or-beginning-search
zle -N down-line-or-beginning-search
bindkey "^[[A" up-line-or-beginning-search # Up
bindkey "^[[B" down-line-or-beginning-search # Down


#-------------------------------------------------------------------------------
# autocompletiong for aws, terraform, etc won't work without this
#-------------------------------------------------------------------------------
# autoload -U +X bashcompinit && bashcompinit
